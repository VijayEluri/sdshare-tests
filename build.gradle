/*
 * Gradle 0.9 build file.
 * 
 * - Download Gradle from <http://www.gradle.org/>
 * - Execute gradle build
 * 
 * TODO: Zip / Tar archives
 *
 */
group = 'org.isotopicmaps.sdshare'
version = '0.1.0'
bundleVersion = version

projectName = 'SDShare Server Tests'
projectDescription = projectName // TODO
projectURL = 'http://github.com/heuer/sdshare-tests'

vendorName = 'Semagia'
vendorURL = 'http://www.semagia.com/'
licenseName = 'Apache Software License, Version 2.0'
licenseURL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

defaultTasks 'clean', 'build'

//TODO: Remove me once <http://jira.codehaus.org/browse/GRADLE-1079> is fixed
eclipseClasspath.variables = [:]
eclipseClasspath.downloadSources = false

gradle.taskGraph.whenReady {taskGraph ->
   if (!taskGraph.hasTask(':release')) {
       version += '-SNAPSHOT'
   }
}

REP_SEMAGIA = 'http://repository.semagia.com/releases/'
REP_TMAPI = 'http://www.tmapi.org/maven-repository/'

repositories {
    mavenCentral()
    mavenRepo urls: REP_TMAPI
    mavenRepo urls: REP_SEMAGIA
}

dependencies {
    compile 'junit:junit:4.8.1', 
            'xom:xom:1.2.5', 
//            'org.tmapi:tmapi:[2.0.2,)',
//            'org.tmapix:tmapix-io:1.0.0',
            'org.slf4j:slf4j-api:(1.5.8,)'
    testRuntime 'org.slf4j:slf4j-jdk14:(1.5.8,)'
}

jar {
      manifest {
        name = projectName
        
        attributes 'Implementation-Version': project.version, 
                    'Implementation-Vendor': project.vendorName, 
                    'Implementation-URL': project.projectURL, 
                    'Sealed': 'false',
                    'Main-Class': 'org.isotopicmaps.sdsharetests.server.RunServerTests',
                    'Class-Path': configurations.runtime.plus(configurations.testRuntime).collect({ 'lib/' + it.name}).join(' ')
      }
}

artifacts {
    archives jar
}

task dist(dependsOn: [assemble, test]) << {
    def base_name = "$archivesBaseName-${project.version}"
    def base = "$distsDir/" + base_name
    def zips = "$buildDir/dist/$base_name"
    def doc_dir = "$buildDir/docs"
    copy {
        into base
        from(doc_dir)
        from(libsDir)
        from(projectDir) {
            include 'README'
            include 'LICENSE.txt'
            include 'CHANGES.txt'
            include 'NOTICE.txt'
        }
        from("$projectDir/src") {
            into 'src'
            exclude '**/*.bak'
        }
        into('lib') {
            from configurations.runtime
            from configurations.testRuntime
        }
    }
    ant {
        tar destfile: zips + '.tar.gz', basedir: distsDir, compression: 'gzip', longfile: 'gnu'
        zip destfile: zips + '.zip', basedir: distsDir
    }
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file://$buildDir/maven-repo/")

        pom.project {
            name project.projectName
            description project.projectDescription
            url project.projectURL
            
            organization {
                name project.vendorName
                url project.vendorURL
            }
            licenses {
               license {
                  name project.licenseName
                  url project.licenseURL
               }
            }
            repositories {
               repository {
                  id 'tmapi'
                  url REP_TMAPI
               }
               repository {
                  id 'semagia-releases'
                  url REP_SEMAGIA
               }
            }
        }
    }
}

